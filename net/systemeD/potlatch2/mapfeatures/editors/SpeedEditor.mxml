<?xml version="1.0" encoding="utf-8"?>
<edit:SingleTagEditor
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:edit="net.systemeD.potlatch2.mapfeatures.editors.*"
	verticalGap="0"
	width="100%"
	toolTip="{fieldDescription}">

  <mx:Label text="{fieldName}:"/>
  <edit:ChoiceComboBox id="inputBox" dataProvider="{choices}" selectedItem="{selectFromTag}"
      dropdownFactory="mx.controls.TileList"
      width="100%" change="value = inputBox.selectedItem.value"
      open="inputBox.dropdown.variableRowHeight = true"
      itemRenderer="net.systemeD.potlatch2.mapfeatures.editors.RestrictionRenderer">
  </edit:ChoiceComboBox>

  <mx:Script><![CDATA[
      import mx.collections.*;
      import mx.utils.*;
      import net.systemeD.potlatch2.mapfeatures.Feature;
      
      private var _choices:ArrayCollection = null;
      private var _unknownChoice:SpeedChoice = null;

      [Bindable(event="factory_set")]
      private function get choices():ArrayCollection {
          if ( _choices == null ) {
              _choices = new ArrayCollection();
              for each(var sp:String in
                  ["5mph", "10mph", "15mph", "20mph", "30mph", "40mph", "50mph", "60mph","70mph",
                   "10", "20", "50", "60", "90", "100", "110", "130"])
                  _choices.addItem(new SpeedChoice(sp));
          }
          return _choices;
      }
      
      [Bindable(event="tag_changed")]
      private function get selectFromTag():Object {
          var tagValue:String = value;
          for each(var choice:SpeedChoice in choices) {
              if ( choice.isTagMatch(tagValue) )
                  return choice;
          }
          
          // set up the unknown choice
          if ( _unknownChoice == null ) {
              _unknownChoice = new SpeedChoice(tagValue);
              _choices.addItem(_unknownChoice);
          }
          _unknownChoice.value = tagValue;
          
          return _unknownChoice;
      }
      
      private function createUnsetChoice():Choice {
          var choice:Choice = new Choice();
          choice.icon = null;
          choice.description = "Field not set";
          choice.label = "<i>Unset</i>";
          choice.value = null;
          return choice;
      }
  ]]></mx:Script>
</edit:SingleTagEditor>

